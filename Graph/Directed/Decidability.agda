open import Relation.Binary using (DecidableEquality)

module Graph.Directed.Decidability {L : Set} {_РЅЪр┤И_ : DecidableEquality L} where
  open import Level renaming (0РёЊ to 0­ЮЊЂ)
  open import Agda.Builtin.Equality

  open import Function using (id)
  
  open import Relation.Nullary using (Dec; yes; no; _├Ќ-dec_; _Ріј-dec_; _Рєњ-dec_)
  open import Relation.Unary using (Pred; _Ріє_; _Ріѕ_; _Рѕф_)
  open import Relation.Binary using (Rel; _РЄњ_)

  open import Data.Empty using (РіЦ; РіЦ-elim)
  open import Data.Bool using (true; false)
  open import Data.Sum using (_Ріј_; injРѓЂ; injРѓѓ; [_,_])
  open import Data.Product using (_├Ќ_; _,_; projРѓЂ; projРѓѓ)

  open import Graph.Directed.Core {L}
  
  V-of? : (g : Graph) Рєњ (x : L) Рєњ Dec (x РѕѕV[ g ])
  V-of? ╬х         x = no (╬╗ xРѓЂ Рєњ xРѓЂ)
  V-of? (v xРѓЂ)    x = xРѓЂ РЅЪр┤И x
  V-of? (gРѓЂ + gРѓѓ) x = (V-of? gРѓЂ x) Ріј-dec (V-of? gРѓѓ x)
  V-of? (gРѓЂ * gРѓѓ) x = (V-of? gРѓЂ x) Ріј-dec (V-of? gРѓѓ x)
  
  E-of? : (g : Graph) Рєњ (x y : L) Рєњ Dec (x Рєд y РѕѕE[ g ])
  E-of?  ╬х        x y = no id
  E-of? (v w)     x y = no id
  E-of? (gРѓЂ + gРѓѓ) x y =  (E-of? gРѓЂ x y) Ріј-dec (E-of? gРѓѓ x y)
  E-of? (gРѓЂ * gРѓѓ) x y = ((E-of? gРѓЂ x y) Ріј-dec (E-of? gРѓѓ x y))
                           Ріј-dec ((V-of? gРѓЂ x) ├Ќ-dec (V-of? gРѓѓ y))

  private
    _Рѕф-Ріє-dec_ : {A : Set} {P Q R : Pred A 0­ЮЊЂ} Рєњ Dec (P Ріє Q) Рєњ Dec (R Ріє Q) Рєњ Dec (P Рѕф R Ріє Q)
    no  PРіѕQ Рѕф-Ріє-dec _       = no ╬╗ PРѕфRРієQ Рєњ PРіѕQ (╬╗ xРѕѕP Рєњ PРѕфRРієQ (injРѓЂ xРѕѕP))
    yes _   Рѕф-Ріє-dec no  RРіѕQ = no ╬╗ PРѕфRРієQ Рєњ RРіѕQ (╬╗ xРѕѕR Рєњ PРѕфRРієQ (injРѓѓ xРѕѕR))
    yes PРієQ Рѕф-Ріє-dec yes RРієQ = yes ╬╗ { (injРѓЂ xРѕѕP) Рєњ PРієQ xРѕѕP ; (injРѓѓ xРѕѕR) Рєњ RРієQ xРѕѕR}

    _Ріј-РЄњ-dec_ : {A : Set} {P Q R : Rel A 0­ЮЊЂ} Рєњ Dec (P РЄњ Q) Рєњ Dec (R РЄњ Q) Рєњ Dec ((╬╗ x y Рєњ P x y Ріј R x y) РЄњ Q)
    no ┬гPРЄњQ Ріј-РЄњ-dec _        = no ╬╗ PРіјRРЄњQ Рєњ ┬гPРЄњQ (╬╗ xPy Рєњ PРіјRРЄњQ (injРѓЂ xPy))
    yes _    Ріј-РЄњ-dec no ┬гRРЄњQ = no ╬╗ PРіјRРЄњQ Рєњ ┬гRРЄњQ (╬╗ xRy Рєњ PРіјRРЄњQ (injРѓѓ xRy))
    yes PРЄњQ Ріј-РЄњ-dec yes RРЄњQ = yes ╬╗ { (injРѓЂ xPy) Рєњ PРЄњQ xPy ; (injРѓѓ xRy) Рєњ RРЄњQ xRy}

    V├ЌVРЄњE? : {gРѓЂ gРѓѓ gРѓЃ : Graph} Рєњ ((x : L) Рєњ Dec (x РѕѕV[ gРѓЂ ])) Рєњ ((y : L) Рєњ Dec (y РѕѕV[ gРѓѓ ]))Рєњ ((x y : L) Рєњ Dec (x Рєд y РѕѕE[ gРѓЃ ]))
                                 Рєњ Dec ((╬╗ x y Рєњ V-of gРѓЂ x ├Ќ V-of gРѓѓ y) РЄњ E-of gРѓЃ)
    V├ЌVРЄњE? {╬х}         _РѕѕVРѓЂ? _РѕѕVРѓѓ? _Рєд_РѕѕEРѓЃ? = yes ╬╗ ()
    V├ЌVРЄњE? {v x} {╬х}   _РѕѕVРѓЂ? _РѕѕVРѓѓ? _Рєд_РѕѕEРѓЃ? = yes ╬╗ ()
    V├ЌVРЄњE? {v x} {v y} _РѕѕVРѓЂ? _РѕѕVРѓѓ? _Рєд_РѕѕEРѓЃ? with x РѕѕVРѓЂ? | y РѕѕVРѓѓ? | x Рєд y РѕѕEРѓЃ?
    ... | no xРЅаx | _      | _ = РіЦ-elim (xРЅаx refl)
    ... | yes _  | no yРЅаy | _ = РіЦ-elim (yРЅаy refl)
    ... | yes _  | yes _  | no ┬гxEРѓЃy = no ╬╗ xРѓЂ Рєњ ┬гxEРѓЃy (xРѓЂ (refl , refl))
    ... | yes _  | yes _  | yes xEРѓЃy = yes ╬╗ { (refl , refl) Рєњ xEРѓЃy}
    V├ЌVРЄњE? {v x} {gРѓё + gРѓЁ} _РѕѕVРѓЂ? _РѕѕVРѓѓ? _Рєд_РѕѕEРѓЃ? with V├ЌVРЄњE? {v x} {gРѓё} _РѕѕVРѓЂ? (V-of? gРѓё) _Рєд_РѕѕEРѓЃ? | V├ЌVРЄњE? {v x} {gРѓЁ} _РѕѕVРѓЂ? (V-of? gРѓЁ) _Рєд_РѕѕEРѓЃ?
    ... | no ┬гx├ЌVРѓёРЄњEРѓЃ | _            = no ╬╗ xРѓЂ Рєњ ┬гx├ЌVРѓёРЄњEРѓЃ ╬╗ { (refl , yРѕѕVРѓё) Рєњ xРѓЂ (refl , (injРѓЂ yРѕѕVРѓё))}
    ... | yes _        | no ┬гx├ЌVРѓЁРЄњEРѓЃ = no ╬╗ xРѓЂ Рєњ ┬гx├ЌVРѓЁРЄњEРѓЃ ╬╗ { (refl , yРѕѕVРѓЁ) Рєњ xРѓЂ (refl , (injРѓѓ yРѕѕVРѓЁ))}
    ... | yes x├ЌVРѓёРЄњEРѓЃ | yes x├ЌVРѓЁРЄњEРѓЃ = yes ╬╗ { (refl , injРѓЂ yРѕѕVРѓё) Рєњ x├ЌVРѓёРЄњEРѓЃ (refl , yРѕѕVРѓё)
                                              ; (refl , injРѓѓ yРѕѕVРѓЁ) Рєњ x├ЌVРѓЁРЄњEРѓЃ (refl , yРѕѕVРѓЁ)}
    V├ЌVРЄњE? {v x} {gРѓё * gРѓЁ} _РѕѕVРѓЂ? _РѕѕVРѓѓ? _Рєд_РѕѕEРѓЃ? with V├ЌVРЄњE? {v x} {gРѓё} _РѕѕVРѓЂ? (V-of? gРѓё) _Рєд_РѕѕEРѓЃ? | V├ЌVРЄњE? {v x} {gРѓЁ} _РѕѕVРѓЂ? (V-of? gРѓЁ) _Рєд_РѕѕEРѓЃ?
    ... | no ┬гx├ЌVРѓёРЄњEРѓЃ | _            = no ╬╗ xРѓЂ Рєњ ┬гx├ЌVРѓёРЄњEРѓЃ ╬╗ { (refl , yРѕѕVРѓё) Рєњ xРѓЂ (refl , (injРѓЂ yРѕѕVРѓё))}
    ... | yes _        | no ┬гx├ЌVРѓЁРЄњEРѓЃ = no ╬╗ xРѓЂ Рєњ ┬гx├ЌVРѓЁРЄњEРѓЃ ╬╗ { (refl , yРѕѕVРѓЁ) Рєњ xРѓЂ (refl , (injРѓѓ yРѕѕVРѓЁ))}
    ... | yes x├ЌVРѓёРЄњEРѓЃ | yes x├ЌVРѓЁРЄњEРѓЃ = yes ╬╗ { (refl , injРѓЂ yРѕѕVРѓё) Рєњ x├ЌVРѓёРЄњEРѓЃ (refl , yРѕѕVРѓё)
                                              ; (refl , injРѓѓ yРѕѕVРѓЁ) Рєњ x├ЌVРѓЁРЄњEРѓЃ (refl , yРѕѕVРѓЁ)}
    V├ЌVРЄњE? {gРѓё + gРѓЁ} {gРѓѓ}  _РѕѕVРѓЂ? _РѕѕVРѓѓ? _Рєд_РѕѕEРѓЃ? with V├ЌVРЄњE? {gРѓё} {gРѓѓ} (V-of? gРѓё) _РѕѕVРѓѓ?  _Рєд_РѕѕEРѓЃ? | V├ЌVРЄњE? {gРѓЁ} {gРѓѓ} (V-of? gРѓЁ) _РѕѕVРѓѓ?  _Рєд_РѕѕEРѓЃ?
    ... | no ┬гVРѓё├ЌVРѓѓРЄњEРѓЃ | _             = no ╬╗ xРѓЂ Рєњ ┬гVРѓё├ЌVРѓѓРЄњEРѓЃ ╬╗ (xРѕѕVРѓё , xРѕѕVРѓѓ) Рєњ xРѓЂ (injРѓЂ xРѕѕVРѓё , xРѕѕVРѓѓ)
    ... | yes _         | no ┬гVРѓЁ├ЌVРѓѓРЄњEРѓЃ = no ╬╗ xРѓЂ Рєњ ┬гVРѓЁ├ЌVРѓѓРЄњEРѓЃ ╬╗ (xРѕѕVРѓЁ , xРѕѕVРѓѓ) Рєњ xРѓЂ (injРѓѓ xРѕѕVРѓЁ , xРѕѕVРѓѓ)
    ... | yes VРѓё├ЌVРѓѓРЄњEРѓЃ | yes VРѓЁ├ЌVРѓѓРЄњEРѓЃ = yes ╬╗ { (injРѓЂ xРѕѕVРѓё , yРѕѕVРѓѓ) Рєњ VРѓё├ЌVРѓѓРЄњEРѓЃ (xРѕѕVРѓё , yРѕѕVРѓѓ)
                                                ; (injРѓѓ xРѕѕVРѓЁ , yРѕѕVРѓѓ) Рєњ VРѓЁ├ЌVРѓѓРЄњEРѓЃ (xРѕѕVРѓЁ , yРѕѕVРѓѓ)}
    V├ЌVРЄњE? {gРѓё * gРѓЁ} {gРѓѓ}  _РѕѕVРѓЂ? _РѕѕVРѓѓ? _Рєд_РѕѕEРѓЃ? with V├ЌVРЄњE? {gРѓё} {gРѓѓ} (V-of? gРѓё) _РѕѕVРѓѓ?  _Рєд_РѕѕEРѓЃ? | V├ЌVРЄњE? {gРѓЁ} {gРѓѓ} (V-of? gРѓЁ) _РѕѕVРѓѓ?  _Рєд_РѕѕEРѓЃ?
    ... | no ┬гVРѓё├ЌVРѓѓРЄњEРѓЃ | _             = no ╬╗ xРѓЂ Рєњ ┬гVРѓё├ЌVРѓѓРЄњEРѓЃ ╬╗ (xРѕѕVРѓё , xРѕѕVРѓѓ) Рєњ xРѓЂ (injРѓЂ xРѕѕVРѓё , xРѕѕVРѓѓ)
    ... | yes _         | no ┬гVРѓЁ├ЌVРѓѓРЄњEРѓЃ = no ╬╗ xРѓЂ Рєњ ┬гVРѓЁ├ЌVРѓѓРЄњEРѓЃ ╬╗ (xРѕѕVРѓЁ , xРѕѕVРѓѓ) Рєњ xРѓЂ (injРѓѓ xРѕѕVРѓЁ , xРѕѕVРѓѓ)
    ... | yes VРѓё├ЌVРѓѓРЄњEРѓЃ | yes VРѓЁ├ЌVРѓѓРЄњEРѓЃ = yes ╬╗ { (injРѓЂ xРѕѕVРѓё , yРѕѕVРѓѓ) Рєњ VРѓё├ЌVРѓѓРЄњEРѓЃ (xРѕѕVРѓё , yРѕѕVРѓѓ)
                                                ; (injРѓѓ xРѕѕVРѓЁ , yРѕѕVРѓѓ) Рєњ VРѓЁ├ЌVРѓѓРЄњEРѓЃ (xРѕѕVРѓЁ , yРѕѕVРѓѓ)}
  
  _РієР▒й?_ : (gРѓЂ gРѓѓ : Graph) Рєњ Dec ((V-of gРѓЂ) Ріє (V-of gРѓѓ))
  ╬х РієР▒й? _ = yes (╬╗ x Рєњ РіЦ-elim x)
  (v x) РієР▒й? ╬х = no (╬╗ VРѓЊРієРѕЁ Рєњ VРѓЊРієРѕЁ refl)
  (v xРѓЂ) РієР▒й? (v xРѓѓ) with xРѓѓ РЅЪр┤И xРѓЂ
  ... | no  xРѓѓРЅаxРѓЂ = no (╬╗ x Рєњ xРѓѓРЅаxРѓЂ (x refl))
  ... | yes refl = yes (╬╗ { refl Рєњ refl })
  (v x) РієР▒й? (gРѓѓ + gРѓЃ) with (v x) РієР▒й? gРѓѓ | (v x) РієР▒й? gРѓЃ
  ... | no  VРѓЊРіѕVРѓѓ | no  VРѓЊРіѕVРѓЃ = no (╬╗ xРѓЂ Рєњ [ (╬╗ xРѕѕVРѓѓ Рєњ VРѓЊРіѕVРѓѓ ╬╗ { refl Рєњ xРѕѕVРѓѓ}) , (╬╗ xРѕѕVРѓЃ Рєњ VРѓЊРіѕVРѓЃ (╬╗ { refl Рєњ xРѕѕVРѓЃ})) ] (xРѓЂ refl))
  ... | no  VРѓЊРіѕVРѓѓ | yes VРѓЊРієVРѓЃ = yes (╬╗ refl Рєњ injРѓѓ (VРѓЊРієVРѓЃ refl))
  ... | yes VРѓЊРієVРѓѓ | Q = yes (╬╗ refl Рєњ injРѓЂ (VРѓЊРієVРѓѓ refl))
  (v x) РієР▒й? (gРѓѓ * gРѓЃ) with (v x) РієР▒й? gРѓѓ | (v x) РієР▒й? gРѓЃ
  ... | no  VРѓЊРіѕVРѓѓ | no  VРѓЊРіѕVРѓЃ = no (╬╗ xРѓЂ Рєњ [ (╬╗ xРѕѕVРѓѓ Рєњ VРѓЊРіѕVРѓѓ ╬╗ { refl Рєњ xРѕѕVРѓѓ}) , (╬╗ xРѕѕVРѓЃ Рєњ VРѓЊРіѕVРѓЃ (╬╗ { refl Рєњ xРѕѕVРѓЃ})) ] (xРѓЂ refl))
  ... | no  VРѓЊРіѕVРѓѓ | yes VРѓЊРієVРѓЃ = yes (╬╗ refl Рєњ injРѓѓ (VРѓЊРієVРѓЃ refl))
  ... | yes VРѓЊРієVРѓѓ | Q = yes (╬╗ refl Рєњ injРѓЂ (VРѓЊРієVРѓѓ refl))
  (gРѓЂ + gРѓѓ) РієР▒й? gРѓЃ = (gРѓЂ РієР▒й? gРѓЃ) Рѕф-Ріє-dec (gРѓѓ РієР▒й? gРѓЃ)
  (gРѓЂ * gРѓѓ) РієР▒й? gРѓЃ = (gРѓЂ РієР▒й? gРѓЃ) Рѕф-Ріє-dec (gРѓѓ РієР▒й? gРѓЃ)

  _Рієр┤▒?_ : (gРѓЂ gРѓѓ : Graph) Рєњ Dec ((E-of gРѓЂ) РЄњ (E-of gРѓѓ))
  ╬х         Рієр┤▒? _  = yes РіЦ-elim
  (v x)     Рієр┤▒? _  = yes РіЦ-elim
  (gРѓЂ + gРѓѓ) Рієр┤▒? gРѓЃ =  (gРѓЂ Рієр┤▒? gРѓЃ) Ріј-РЄњ-dec (gРѓѓ Рієр┤▒? gРѓЃ)
  (gРѓЂ * gРѓѓ) Рієр┤▒? gРѓЃ = ((gРѓЂ Рієр┤▒? gРѓЃ) Ріј-РЄњ-dec (gРѓѓ Рієр┤▒? gРѓЃ)) Ріј-РЄњ-dec
                     (V├ЌVРЄњE? {gРѓЂ} {gРѓѓ} (V-of? gРѓЂ) (V-of? gРѓѓ) (E-of? gРѓЃ))

  _РієрхЇ?_ : (gРѓЂ gРѓѓ : Graph) Рєњ Dec (gРѓЂ РієрхЇ gРѓѓ)
  gРѓЂ РієрхЇ? gРѓѓ = (gРѓЂ РієР▒й? gРѓѓ) ├Ќ-dec (gРѓЂ Рієр┤▒? gРѓѓ)

  _РЅЪ_ : (gРѓЂ gРѓѓ : Graph) Рєњ Dec (gРѓЂ РЅАрхЇ gРѓѓ)
  gРѓЂ РЅЪ gРѓѓ = (gРѓЂ РієрхЇ? gРѓѓ) ├Ќ-dec (gРѓѓ РієрхЇ? gРѓЂ)
